module nabl2/shared/common/Relations

imports

  nabl2/shared/common/Identifiers

template options

  keyword -/- [a-zA-Z0-9\'\_]

context-free sorts
  NaBL2-Reflexivity NaBL2-Symmetry NaBL2-Transitivity NaBL2-Relation NaBL2-RelationRef

context-free syntax

  NaBL2-Reflexivity.Reflexive       = "reflexive"
  NaBL2-Reflexivity.Irreflexive     = "irreflexive"
  
  NaBL2-Symmetry.Symmetric          = "symmetric" 
  NaBL2-Symmetry.AntiSymmetric      = "anti-symmetric" 
  
  NaBL2-Transitivity.Transitive     = "transitive"
  NaBL2-Transitivity.AntiTransitive = "anti-transitive"

  NaBL2-Relation.Relation           = NaBL2-RelationId

  NaBL2-RelationRef.DefaultRelation =
  NaBL2-RelationRef                 = NaBL2-Relation

sorts
  NaBL2-RelationBuildOp NaBL2-RelationCheckOp NaBL2-RelationFunction NaBL2-OpRelation

syntax

  NaBL2-RelationRef-CF.ExtRelation = "`" NaBL2-StrategyId-LEX "`"

  NaBL2-RelationBuildOp-CF = "<" NaBL2-RelationRef-CF "!"
  NaBL2-RelationCheckOp-CF = "<" NaBL2-RelationRef-CF "?"

  NaBL2-RelationFunction-CF.Lub = NaBL2-OpRelation-CF "lub"
  NaBL2-RelationFunction-CF.Glb = NaBL2-OpRelation-CF "glb"

  NaBL2-OpRelation-CF.DefaultOpRelation =
  NaBL2-OpRelation-CF.OpRelation        = NaBL2-RelationId-LEX "."

context-free sorts
  NaBL2-VariancePattern NaBL2-Variances

context-free syntax

  NaBL2-VariancePattern.ListVariant  = <[<NaBL2-Variance>]>
  NaBL2-VariancePattern.TupleVariant = <(<{NaBL2-Variance ","}*>)>
 
  NaBL2-Variances = {NaBL2-Variance ","}*

sorts
  NaBL2-Variance

syntax

  NaBL2-VariancePattern-CF.OpVariant = NaBL2-OpId-LEX "(" LAYOUT?-CF NaBL2-Variances-CF LAYOUT?-CF ")"

  NaBL2-Variance-CF.Invar     = "="
  NaBL2-Variance-CF.Covar     = "+" NaBL2-RelationRef-CF
  NaBL2-Variance-CF.Contravar = "-" NaBL2-RelationRef-CF
  
lexical syntax

  NaBL2-Keyword = "reflexive" | "irreflexive"
          | "symmetric" | "anti-symmetric"
          | "transitive" | "anti-transitive"

  NaBL2-FunctionId = "lub" | "glb" {reject}
