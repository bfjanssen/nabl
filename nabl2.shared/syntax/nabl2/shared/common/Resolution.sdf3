module nabl2/shared/common/Resolution

imports

  nabl2/shared/common/Identifiers
  nabl2/shared/common/ScopeGraph

template options

  keyword -/- [a-zA-Z0-9\'\_]

context-free syntax

  NaBL2-Strategy.Search       = <search>
  NaBL2-Strategy.Environments = <environments>

  NaBL2-Relevance.Relevant   = <relevant>
  NaBL2-Relevance.Irrelevant = <irrelevant>

context-free syntax

  NaBL2-Label.Label = NaBL2-LabelId
 
  NaBL2-PathLabel   = NaBL2-Label
  NaBL2-PathLabel   = NaBL2-BuiltinPathLabel
  
  NaBL2-BuiltinPathLabel.P = "P"
  NaBL2-BuiltinPathLabel.I = "I"
 
  NaBL2-AnyLabel          = NaBL2-PathLabel
  NaBL2-AnyLabel          = NaBL2-BuiltinAnyLabel

  NaBL2-BuiltinAnyLabel.D = "D"

  NaBL2-Path = <[<{NaBL2-Step " "}*>]>

  NaBL2-Step.D = <<NaBL2-Scope> D <NaBL2-Occurrence>>
  NaBL2-Step.E = <<NaBL2-Scope> E(<NaBL2-PathLabel>)>
  NaBL2-Step.N = <<NaBL2-Scope> N(<NaBL2-PathLabel>,<NaBL2-Occurrence>,<NaBL2-Path>)>
 
context-free syntax

  NaBL2-LabelRE.Empty      = <0>
  NaBL2-LabelRE.Epsilon    = <e>
  NaBL2-LabelRE            = NaBL2-PathLabel
  NaBL2-LabelRE.Closure    = <<NaBL2-LabelRE>*>
  NaBL2-LabelRE.Concat     = <<NaBL2-LabelRE> <NaBL2-LabelRE>>     {right}
  NaBL2-LabelRE.And        = <<NaBL2-LabelRE> & <NaBL2-LabelRE>>   {left}
  NaBL2-LabelRE.Or         = <<NaBL2-LabelRE> | <NaBL2-LabelRE>>   {left}
  NaBL2-LabelRE            = <(<NaBL2-LabelRE>)>             {bracket}

  NaBL2-LabelOrder.Lt      = [[NaBL2-AnyLabel] < [NaBL2-AnyLabel]]

context-free priorities

    NaBL2-LabelRE.Closure
  > NaBL2-LabelRE.Concat
  > NaBL2-LabelRE.And
  > NaBL2-LabelRE.Or

context-free restrictions

  NaBL2-BuiltinPathLabel -/- [a-zA-Z0-9\_\']
  NaBL2-BuiltinAnyLabel  -/- [a-zA-Z0-9\_\']
