module statix/lang/PreCompiled

imports

  statix/lang/Common
  statix/lang/Core

lexical sorts
  ENVID

context-free sorts

  PreCompiledQuery

  State
  StateId
  StateRef

  RStep
  RExp
  RVar
  RCond

lexical syntax

  ENVID = [a-z] [a-zA-Z0-9\$\_\-]*

lexical restrictions

  ENVID -/- [a-zA-Z0-9\$\_\-]

context-free syntax

  Constraint.CPreCompiledQuery = [
    compiled query [QueryTarget]
     [QueryFilter]
     [QueryMin]
      in [Term] |-> [Term]
    to
      states
        [{State "\n\n"}*]

      initial [StateRef]
    [Message]
  ]


context-free syntax // States

  State.State = <
    <StateId>:
      <{RStep "\n"}+>
      return <RVar>
  >

  StateId  = LCID
  StateRef = LCID

context-free syntax // Resolution steps

  RStep.Step     = <<RVar> := <RExp>>

context-free syntax // Resolution expressions

  RExp.Resolve   = <resolve>

  RExp.SubEnv    = <subenv <RelRef> <StateRef>>

  RExp.Merge     = <merge(<{RVar ", "}*>)>

  RExp.Shadow    = <shadow(<RVar>, <RVar>)>

  RExp.CExp      = <if not empty <RVar> else <RExp>>

  RExp           = RVar // TODO needed? Only used in `CExp`, but can single-label (l) environments ever require recomputation, as in any case, the lL environment will already be available?

  RVar.RVar      = ENVID
